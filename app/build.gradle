apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.firebase.crashlytics'

android {
  compileSdkVersion DefaultConfig.compileSdk

  ndkVersion "21.3.6528147"

  defaultConfig {
    applicationId App.swabbr

    minSdkVersion DefaultConfig.minSdk
    targetSdkVersion DefaultConfig.targetSdk

    versionCode Releases.versionCode
    versionName Releases.versionName

    multiDexEnabled true
    signingConfig signingConfigs.debug

  }

  buildFeatures {
    buildConfig = true
  }

  lintOptions {
    abortOnError false
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8.toString()
  }

  buildTypes {
    release {
      minifyEnabled true

      buildConfigField "String", "API_ENDPOINT", "\"https://swabbr.azurewebsites.net/api/v1/\""
    }
    debug {
      debuggable true
      signingConfig signingConfigs.debug

      buildConfigField "String", "API_ENDPOINT", "\"https://swabbr-api-development.azurewebsites.net/api/1/\""
    }
  }

  sourceSets {
    main {
      file('src/main/res-screen')
        .listFiles()
        .each { res.srcDirs += it.path }
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }
}

dependencies {
  implementation project(Modules.cache)
  implementation project(Modules.network)
  implementation project(Modules.presentation)
  implementation project(Modules.liveVideoBroadcasterSDK)

  implementation Libs.kotlin_stdlib_jdk8
  implementation Libs.kotlin_reflect

  implementation Libs.jwtdecode
  implementation Libs.runtime_permission_kotlin

  // Koin
  implementation Libs.koin_android
  implementation Libs.koin_androidx_viewmodel

  // Retrofit
  implementation Libs.retrofit

  // Moshi
  implementation Libs.moshi_kotlin
  implementation Libs.converter_moshi
  implementation Libs.moshi_adapters

  // Rxjava
  implementation Libs.rxjava
  implementation Libs.rxkotlin
  implementation Libs.okhttp
  implementation Libs.logging_interceptor
  implementation Libs.adapter_rxjava2
  implementation Libs.work_rxjava2

  // UI
  implementation Libs.swiperefreshlayout
  implementation Libs.constraintlayout
  implementation Libs.camera_camera2
  implementation Libs.viewpager2
  implementation Libs.material
  implementation Libs.exoplayer
  implementation Libs.imagepicker
  implementation Libs.circleimageview
  implementation Libs.leonidslib

  // AndroidX
  implementation Libs.work_runtime_ktx
  implementation Libs.lifecycle_extensions
  implementation Libs.core_ktx
  implementation Libs.appcompat
  implementation Libs.fragment_ktx

  // Navigation component
  implementation Libs.navigation_fragment_ktx
  implementation Libs.navigation_ui_ktx

  implementation 'com.gu.android:toolargetool:0.2.1@aar'

  // Firebase
  // Import the BoM for the Firebase platform
  implementation platform('com.google.firebase:firebase-bom:25.12.0')

  implementation Libs.firebase_crashlytics
  implementation Libs.firebase_analytics
  implementation Libs.firebase_messaging

  debugImplementation Libs.leakcanary_android

  // Testing dependencies
  testImplementation Libs.junit
  testImplementation Libs.assertj_core
  testImplementation Libs.mockito_kotlin
  testImplementation Libs.mockito_inline
  testImplementation Libs.core_testing
}

repositories {
  google()
    mavenCentral()
}

apply plugin: 'com.google.gms.google-services'
